apply(plugin: "dev.architectury.loom")
apply(plugin: "maven-publish")
apply(plugin: "com.replaymod.preprocess")

int mcVersion = 1

preprocess {
    mcVersion = vars.get().get("MC")
    // tabIndentation.set(false)
}


repositories {
    mavenCentral()
    maven { url 'https://maven.nova-committee.cn/releases'}
    maven { url 'https://maven.mohistmc.com/' }

}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"


    implementation forgeRuntimeLibrary ("dev.vankka:dependencydownload-runtime:1.3.1") {exclude module: 'jar-relocator'}
    implementation forgeRuntimeLibrary ("dev.vankka:dependencydownload-common:1.3.1")
    implementation forgeRuntimeLibrary (include("cn.evole.onebot:OneBot-Client:${onebot_client_version}")){transitive = false}
    implementation forgeRuntimeLibrary (include("cn.evole.config:AtomConfig-Toml:${toml_version}")){transitive = false}

    if (mcVersion == 11605) include ("org.slf4j:slf4j-api:2.0.7")
    compileOnly ("org.projectlombok:lombok:1.18.30")
    annotationProcessor ("org.projectlombok:lombok:1.18.30")

}

remapJar {
    remapperIsolation = true
}


tasks.register('copyJars', Copy) {
    delete "$projectDir/build/libs"
    delete "$rootDir/build/libs"
    from "$projectDir/build/libs"
    into "$rootDir/build/libs"
}

buildNeeded.finalizedBy("copyJars")


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name + "-" + project.minecraft_dependency

processResources {
    outputs.upToDateWhen { false }

    filesMatching("META-INF/mods.toml") {
        expand([
                "minecraft_version_range"   : project.minecraft_version_range,
                "mod_id"              : project.mod_id,
                "mod_name"            : project.archives_base_name,
                "mod_version"         : project.version
        ])

    }
}

tasks.withType(JavaCompile).configureEach {
    options.setEncoding("UTF-8")
}

jar {
    from("../../../LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}


publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from(components.java)
            version("${project.version}")
            artifactId("${project.archives_base_name}-${project.minecraft_dependency}")
            groupId("${project.maven_group}")
        }
    }

    repositories {
        mavenLocal()
        if (System.getenv('MAVEN_USERNAME') != null && System.getenv('MAVEN_PASSWORD') != null) {
            maven {
                name 'release'
                url = 'https://maven.nova-committee.cn/releases'

                credentials {
                    username System.getenv('MAVEN_USERNAME')
                    password System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }
}